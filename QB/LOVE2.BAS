DEFINT A-Z
DECLARE FUNCTION n$ (n%)
DECLARE SUB TX (s AS STRING)

TYPE WAVHeaderType
  RIFFID AS STRING*4 'should be 'RIFF'
  RIFFLEN AS LONG 'rept. chunk id and size then chunk data
  WAVID AS STRING*4 'should be 'WAVE'
  FMTID AS STRING*4
  FMTLEN AS LONG 'FMT ' chunk - common fields
  FMTTAG AS INTEGER ' word - format category e.g. 0x0001=PCM
  CHANNELS AS INTEGER ' word - number of Channels 1=mono 2=stereo
  FREQ AS LONG 'dword - sampling rate e.g. 44100Hz
  BYTESSEC AS LONG 'dword - to estimate buffer size
  ALIGN AS INTEGER ' word - buffer size must be int. multiple of this
  FMTSPEC AS INTEGER ' word
  DATAID AS STRING*4
  DATALEN AS LONG
END TYPE

TYPE SBType
  STAT AS INTEGER
  VER0 AS INTEGER
  VER1 AS INTEGER
  BASEPORT AS INTEGER
  IRQ AS INTEGER
  DMA AS INTEGER
  HDMA AS INTEGER
  PGPORT AS INTEGER
  ADDPORT AS INTEGER
  LENPORT AS INTEGER
  MODEREG AS INTEGER
  S16 AS INTEGER
END TYPE

CONST BUFLEN=8192

TYPE PLAYWAVType
  FILENUM AS INTEGER
  STAT AS INTEGER
  HDR AS WAVHeaderType
  FPOS AS LONG
  BUFUSE AS INTEGER
  BUF0DLEN AS INTEGER
  BUF0 AS STRING*BUFLEN
  BUF1DLEN AS INTEGER
  BUF1 AS STRING*BUFLEN
END TYPE

CONST WAVHeaderSize=45

DIM SHARED ASM(100)
COMMON SB AS SBType,PWAV AS PLAYWAVType
WAVPLAY=1

DEF SEG=VARSEG(ASM(0))
BLOAD "UTIL.BIN",VARPTR(ASM(0))
ST=0:CALL ABSOLUTE(ST,VARPTR(ASM(0))+ASM(0))
IF ST=0 THEN PRINT "Please run UCDOS and TX first.":END
IF ST=1 THEN PRINT "Please run TX first.":END
IF ST=3 THEN PRINT "256 color support required.":END
DEF SEG

PI#=3.141592654#
OPEN "LPT3" FOR OUTPUT AS #1

CALL InitSoundCard(SB)
CALL LoadWAV(PWAV,SB,"LOVE2.WAV")

FONTSIZE=98:X0=10:Y0=40:HEARTX=320:HEARTY=10:HEARTW=50:HEARTH=150
TX ("M10CU1,0KB1,0CL0CO7B0,0,640,480RE0,0,LOVE2.PCX${%1=3@"+n$(FONTSIZE)+","+n$(FONTSIZE)+"}")
FOR DY=-1 TO 1: FOR DX=-1 TO 1
  IF DX<>0 OR DY<>0 THEN
    TX ("{(3|"+n$(DY+Y0)+"-"+n$(DX+X0)+"石头-"+n$(DX+638-X0-FONTSIZE*2)+"春婷}")
  END IF
NEXT DX: NEXT DY
TX ("{(11|"+n$(Y0)+"-"+n$(X0)+"石头-"+n$(638-X0-FONTSIZE*2)+"春婷}")
X$=n$(HEARTX-HEARTW): Y$=n$(HEARTY+HEARTW): R$=n$(HEARTW)
TX("CO13E"+X$+","+Y$+",0,180,"+R$+","+R$+",2")
TX("CO5E"+X$+","+Y$+",0,180,"+R$+","+R$+",0")
X$=n$(HEARTX+HEARTW)
TX("CO13E"+X$+","+Y$+",0,180,"+R$+","+R$+",2")
TX("CO5E"+X$+","+Y$+",0,180,"+R$+","+R$+",0")
FOR Y0=1 TO HEARTH
  X=INT(HEARTW*2*COS(PI#*Y0/2/HEARTH)+.5)
  X$=n$(HEARTX-X): X2$=n$(HEARTX+X): Y$=n$(HEARTY+HEARTW+Y0)
  TX("CO13L"+X$+","+Y$+","+X2$+","+Y$)
  CALL PlayWAVTask(PWAV,SB)
  TX("CO5D"+X$+","+Y$+"D"+X2$+","+Y$)
  CALL PlayWAVTask(PWAV,SB)
NEXT Y0

WHILE INKEY$="":
  CALL PlayWAVTask(PWAV,SB)
WEND

CALL ResetDSP(SB)
TX ("KB1,1M3CU1,1")
CLOSE
END

FUNCTION n$ (A AS INTEGER)
n$ = LTRIM$(RTRIM$(STR$(A)))
END FUNCTION

SUB TX (s AS STRING)
PRINT #1, CHR$(14); "["; s; "]";
END SUB

SUB LoadWAV(PWAV AS PLAYWAVType,SB AS SBType,WAVFILE AS STRING)
  STATIC S16
  IF SB.STAT<>1 THEN PWAV.STAT=-1:EXIT SUB
  PWAV.STAT=0
  PWAV.FILENUM=FREEFILE
  OPEN WAVFILE FOR BINARY AS PWAV.FILENUM
  IF LOF(PWAV.FILENUM)=0 THEN
    CLOSE PWAV.FILENUM:KILL WAVFILE:PWAV.FILENUM=-1:PWAV.STAT=-2:EXIT SUB
  END IF
  GET PWAV.FILENUM,1,PWAV.HDR
  IF UCASE$(PWAV.HDR.RIFFID)<>"RIFF" THEN PWAV.STAT=-3:EXIT SUB
  IF UCASE$(PWAV.HDR.WAVID)<>"WAVE" THEN PWAV.STAT=-4:EXIT SUB
  IF PWAV.HDR.FMTTAG<>1 THEN PWAV.STAT=-5:EXIT SUB 'Not PCM format
  IF PWAV.HDR.FMTSPEC=16 THEN CALL SetMode(SB,1) ELSE CALL SetMode(SB,0)
  PWAV.STAT=0
END SUB

SUB PlayWAVTask(PWAV AS PLAYWAVType,SB AS SBType)
  STATIC DATALEN AS INTEGER,BUFADDR AS LONG
  IF (PWAV.STAT=1 AND DMADone(SB)=0) OR PWAV.STAT=2 OR PWAV.STAT<0 OR SB.STAT<>1 THEN
    EXIT SUB
  ELSEIF PWAV.STAT=0 THEN
    PWAV.STAT=1
    PWAV.BUFUSE=0
    SEEK PWAV.FILENUM,WAVHeaderSize
    PWAV.FPOS=LOC(PWAV.FILENUM)
    GET PWAV.FILENUM,,PWAV.BUF0
    PWAV.BUF0DLEN=LOC(PWAV.FILENUM)-PWAV.FPOS
    PWAV.FPOS=LOC(PWAV.FILENUM)
  END IF
  IF PWAV.BUFUSE=1 THEN
    BUFADDR=INT2ULONG&(VARSEG(PWAV.BUF1))*16+INT2ULONG&(VARPTR(PWAV.BUF1))
    DATALEN=PWAV.BUF1DLEN-1
  ELSE
    BUFADDR=INT2ULONG&(VARSEG(PWAV.BUF0))*16+INT2ULONG&(VARPTR(PWAV.BUF0))
    DATALEN=PWAV.BUF0DLEN-1
  END IF
  IF DATALEN<0 THEN PWAV.STAT=0:EXIT SUB

  IF SB.S16 THEN
    OUT &HD8,0 'clear flip flop
    OUT &HD6,SB.MODEREG
    OUT SB.ADDPORT,(BUFADDR\2) AND 255
    OUT SB.ADDPORT,(BUFADDR\512) AND 255
    OUT SB.PGPORT,(BUFADDR\131072)*2
    OUT SB.LENPORT,(DATALEN\2) AND 255
    OUT SB.LENPORT,(DATALEN\512) AND 255
    OUT &HD4, SB.HDMA-4
  ELSE
    OUT &HA,&H4+SB.DMA
    OUT &HC,&H0
    OUT &HB,SB.MODEREG
    OUT SB.ADDPORT,(BUFADDR) AND 255
    OUT SB.ADDPORT,(BUFADDR\256) AND 255
    OUT SB.PGPORT,BUFADDR\65536
    OUT SB.LENPORT,DATALEN AND 255
    OUT SB.LENPORT,(DATALEN\256) AND 255
    OUT &HA,SB.DMA
  END IF
  CALL WriteDSP(SB,&H41)
  CALL WriteDSP(SB,PWAV.HDR.FREQ\256)
  CALL WriteDSP(SB,PWAV.HDR.FREQ AND 255)
  IF SB.S16 THEN
    CALL WriteDSP(SB,&HB0) '16 bit DAC, single cycle, FIFO off
    IF PWAV.HDR.CHANNELS=2 THEN 'subtract 10h for unsigned
      CALL WriteDSP(SB,&H30) '30h=Mode byte for 16 bit signed stereo
    ELSE
      CALL WriteDSP(SB,&H10) '10h=Mode byte for 16 bit signed mono
    END IF
    CALL WriteDSP(SB,(DATALEN\2) AND 255)
    CALL WriteDSP(SB,(DATALEN\512) AND 255)
  ELSE
    CALL WriteDSP(SB,&HC0) '8 bit DAC, single cycle, FIFO off
    IF PWAV.HDR.CHANNELS=2 THEN
      CALL WriteDSP(SB,&H20) '20h=Mode byte for 8 bit unsigned stereo
    ELSE
      CALL WriteDSP(SB,&H0)  '0h=Mode byte for 8 bit unsigned mono
    END IF
    CALL WriteDSP(SB,(DATALEN) AND 255)
    CALL WriteDSP(SB,(DATALEN\256) AND 255)
  END IF
  PWAV.BUFUSE=(NOT PWAV.BUFUSE) AND 1
  IF PWAV.BUFUSE=1 THEN
    GET PWAV.FILENUM,,PWAV.BUF1
    PWAV.BUF1DLEN=LOC(PWAV.FILENUM)-PWAV.FPOS
  ELSE
    GET PWAV.FILENUM,,PWAV.BUF0
    PWAV.BUF0DLEN=LOC(PWAV.FILENUM)-PWAV.FPOS
  END IF
  PWAV.FPOS=LOC(PWAV.FILENUM)
END SUB

SUB InitSoundCard(SB AS SBType)
  STATIC ENV$
  SB.STAT=0:SB.BASEPORT=220:SB.IRQ=5:SB.DMA=1:SB.HDMA=5
  ENV$=UCASE$(ENVIRON$("BLASTER"))
  FOR I=1 TO LEN(ENV$)
    SELECT CASE MID$(ENV$,I,1)
      CASE "A"
        SB.BASEPORT=VAL("&H"+MID$(ENV$,I+1,3))
      CASE "I"
        SB.IRQ=VAL(MID$(ENV$,I+1,1))
      CASE "D"
        SB.DMA=VAL(MID$(ENV$,I+1,1))
      CASE "H"
        SB.HDMA=VAL(MID$(ENV$,I+1,1))
    END SELECT
  NEXT I
  IF SB.DMA<0 OR SB.DMA>3 OR SB.HDMA<4 OR SB.HDMA>7 THEN
    EXIT SUB 'Invalid Parameter
  END IF
  CALL ResetDSP(SB)
  IF SB.STAT=-1 THEN EXIT SUB 'No sound card
  CALL WriteDSP(SB,&HE1)
  SB.VER0=ReadDSP(SB)
  SB.VER1=ReadDSP(SB)
  IF SB.VER0<4 THEN SB.STAT=-2:EXIT SUB 'Pre-SB16 sound card not supported
END SUB

SUB SetMode(SB AS SBType,S16 AS INTEGER)
  IF S16>0 THEN
    SB.S16=1
    SELECT CASE SB.HDMA
      CASE 4
        SB.PGPORT=&H0:SB.ADDPORT=&HC0:SB.LENPORT=&HC2:SB.MODEREG=&H48
      CASE 5
        SB.PGPORT=&H8B:SB.ADDPORT=&HC4:SB.LENPORT=&HC6:SB.MODEREG=&H49
      CASE 6
        SB.PGPORT=&H89:SB.ADDPORT=&HC8:SB.LENPORT=&HCA:SB.MODEREG=&H4A
      CASE 7
        SB.PGPORT=&H8A:SB.ADDPORT=&HCC:SB.LENPORT=&HCE:SB.MODEREG=&H4B
      CASE ELSE
        EXIT SUB
    END SELECT
  ELSE
    SB.S16=0
    SELECT CASE SB.DMA
      CASE 0
        SB.PGPORT=&H87:SB.ADDPORT=&H0:SB.LENPORT=&H1:SB.MODEREG=&H48
      CASE 1
        SB.PGPORT=&H83:SB.ADDPORT=&H2:SB.LENPORT=&H3:SB.MODEREG=&H49
      CASE 2
        SB.PGPORT=&H81:SB.ADDPORT=&H4:SB.LENPORT=&H5:SB.MODEREG=&H4A
      CASE 3
        SB.PGPORT=&H82:SB.ADDPORT=&H6:SB.LENPORT=&H7:SB.MODEREG=&H4B
      CASE ELSE
        EXIT SUB
    END SELECT
  END IF
END SUB

SUB ResetDSP(SB AS SBType)
  STATIC T#
  IF SB.STAT=1 THEN
    T#=TIMER:WHILE (TIMER-T#)<1.0 AND DMADone(SB)=0:WEND
  END IF
  SB.STAT=0
  OUT SB.BASEPORT+&H6,1:CT=0
  DO
    OUT SB.BASEPORT+&H6,0
    STAT=INP(SB.BASEPORT+&HE)
    STAT=INP(SB.BASEPORT+&HA)
    IF STAT=&HAA THEN SB.STAT=1:EXIT DO
    CT=CT+1
  LOOP WHILE CT<100
END SUB

SUB WriteDSP(SB AS SBType,BYTE AS INTEGER)
  WHILE INP(SB.BASEPORT+12) AND &H80:WEND
  OUT SB.BASEPORT+12,BYTE
END SUB

FUNCTION ReadDSP(SB AS SBType)
  STATIC DSPIN AS INTEGER
  WAIT (SB.BASEPORT+&HE),&H80
  DO: DSPIN=INP(SB.BASEPORT+10): LOOP UNTIL DSPIN<>&HAA
  ReadDSP=DSPIN
END FUNCTION

FUNCTION DMADone(SB AS SBType)
  STATIC LO AS INTEGER,HI AS INTEGER,ACK AS INTEGER
  LO=INP(SB.LENPORT):HI=INP(SB.LENPORT)
  IF HI=255 AND LO=255 THEN ACK=INP(SB.BASEPORT+&HF):DMADone=-1 ELSE DMADone=0
END FUNCTION

FUNCTION INT2ULONG&(V AS INTEGER)
  IF V<0 THEN INT2ULONG&=CLNG(V+65536) ELSE INT2ULONG&=CLNG(V)
END FUNCTION

