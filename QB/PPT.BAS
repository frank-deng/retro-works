DEFINT A-Z

DECLARE SUB INITENV()
DECLARE SUB LoadData (FILENAME AS STRING)
DECLARE SUB MainLoop ()
DECLARE SUB ProcessLine (MODE AS STRING, DATATEXT AS STRING)
DECLARE SUB DisplaySlide (ID AS INTEGER)
DECLARE FUNCTION GetSlideIdx% (n AS STRING)
DECLARE SUB ProcessTarget (TARGET AS STRING, Ext AS INTEGER)
DECLARE SUB ProcessKeyboard (K AS STRING)
DECLARE FUNCTION n$ (n%)
DECLARE SUB TX (s AS STRING)
DECLARE SUB DrawText (x AS INTEGER, y AS INTEGER, text AS STRING)

TYPE SlideT
  SNAME AS STRING*40
  LSTART AS INTEGER
  LEND AS INTEGER
END TYPE
TYPE ActionT
  SLIDE AS INTEGER
  EXTERNAL AS INTEGER
  TARGET AS STRING * 40
  TRIGGER AS STRING * 10
END TYPE
TYPE FontFormatT
  X AS INTEGER
  Y AS INTEGER
  W AS INTEGER
  H AS INTEGER
  FASC AS INTEGER
  FHZK AS INTEGER
  ATTR AS INTEGER
  SP AS INTEGER
  CL AS INTEGER
END TYPE
TYPE FontDataT
  CH0 AS STRING * 1
  CH1 AS STRING * 1
  FONT AS INTEGER
  W AS INTEGER
  H AS INTEGER
  TOP AS INTEGER
  BOTTOM AS INTEGER
  ATTR AS INTEGER
  BUFLEN AS INTEGER
END TYPE
TYPE WAVHeaderType
  RIFFID AS STRING*4
  RIFFLEN AS LONG
  WAVID AS STRING*4
  FMTID AS STRING*4
  FMTLEN AS LONG
  FMTTAG AS INTEGER
  CHANNELS AS INTEGER
  FREQ AS LONG
  BYTESSEC AS LONG
  ALIGN AS INTEGER
  FMTSPEC AS INTEGER
  DATAID AS STRING*4
  DATALEN AS LONG
END TYPE
TYPE SBType
  STAT AS INTEGER
  VER0 AS INTEGER
  VER1 AS INTEGER
  BASEPORT AS INTEGER
  IRQ AS INTEGER
  DMA AS INTEGER
  HDMA AS INTEGER
  PGPORT AS INTEGER
  ADDPORT AS INTEGER
  LENPORT AS INTEGER
  MODEREG AS INTEGER
  S16 AS INTEGER
END TYPE
CONST WAVBUFLEN=1024
TYPE PLAYWAVType
  FILENUM AS INTEGER
  STAT AS INTEGER
  HDR AS WAVHeaderType
  FPOS AS LONG
  BUFUSE AS INTEGER
  BUF0DLEN AS INTEGER
  BUF0 AS STRING*WAVBUFLEN
  BUF1DLEN AS INTEGER
  BUF1 AS STRING*WAVBUFLEN
END TYPE
CONST WAVHeaderSize=45
CONST SLIDEMAX=64

COMMON SHARED FFMT AS FontFormatT
COMMON SHARED SLIDECNT, ACTIONCNT, CURRENTSLIDE, ACTION$, INITCMD AS STRING, FILENAME$
COMMON SHARED NOTELEN AS INTEGER,NOTECUR AS INTEGER
COMMON SHARED SB AS SBType,PWAV AS PLAYWAVType
DIM SHARED ASM(100) AS INTEGER,BITMAP(8194) AS INTEGER
DIM SHARED SLIDE(SLIDEMAX) AS SlideT,CONTENT(2048) AS STRING,ACTIONARR(512) AS ActionT
DIM SHARED MUSIC(SLIDEMAX) AS STRING,WAV(SLIDEMAX) AS STRING
DIM SHARED NOTES(64) AS STRING

DEF SEG=VARSEG(ASM(0))
BLOAD "UTIL.BIN",VARPTR(ASM(0))
ST=0:CALL ABSOLUTE(ST,VARPTR(ASM(0))+ASM(0))
IF ST=0 THEN PRINT "Please run UCDOS and TX first.":END
IF ST=1 THEN PRINT "Please run TX first.":END
DEF SEG

FFMT.W=16
FFMT.H=16
FFMT.ATTR=1
FFMT.CL=7
NOTELEN=0:NOTECUR=0
PWAV.FILENUM=-1
OPEN "LPT3" FOR OUTPUT AS #1
CALL InitSoundCard(SB)
CLS
INPUT "请输入文件名：", FILENAME$
IF FILENAME$ = "" THEN FILENAME$ = "PPT.TXT"
PRINT "加载中……"
CALL LoadData(FILENAME$)
CALL MainLoop
CALL TX("KB1,1M3CU1,1")
IF SB.STAT=1 THEN CALL ResetDSP(SB)
CLOSE
END

ERROREND:
CALL TX("KB1,1M3CU1,1")
IF SB.STAT=1 THEN CALL ResetDSP(SB)
CLOSE
ON ERROR GOTO 0

SUB TX (s AS STRING)
    PRINT #1, CHR$(14); "["; s; "]";
END SUB
SUB DrawStr(STR AS STRING, X1 AS INTEGER, Y1 AS INTEGER, W AS INTEGER, H AS INTEGER, FONTASC AS INTEGER, FONTHZK AS INTEGER, ATTR AS INTEGER, DIST AS INTEGER, C AS INTEGER)
  I=0: L=LEN(STR): XA=X1: FONT=0
  WHILE I<L
    CH$=MID$(STR,I+1,1): FONT=FONTASC: WA=W: HA=H
    IF ASC(CH$)>=&H80 THEN
      I=I+1: CH$=CH$+MID$(STR,I+1,1): FONT=FONTHZK
    END IF
    CALL GetChar(CH$,FONT,WA,HA,ATTR)
    IF ((ATTR AND 8)=0 AND (ATTR AND 1)=0) OR ((ATTR AND 8)<>0 AND (ATTR AND 1)<>0) THEN
      CALL DrawChar(HA,WA,XA,Y1,C,BITMAP())
      WA=HA
    ELSE
      CALL DrawChar(WA,HA,XA,Y1,C,BITMAP())
    END IF
    DX=WA+DIST: IF DX>0 THEN XA=XA+DX
    I=I+1
  WEND
END SUB
SUB GetChar(STR AS STRING,FONT AS INTEGER,W AS INTEGER,H AS INTEGER,ATTR AS INTEGER)
  STATIC Param AS FontDataT
  IF ASC(MID$(STR,1,1)) < &H80 THEN
    Param.CH0=STR: Param.CH1=CHR$(0)
    Param.W=W
  ELSE
    Param.CH0=MID$(STR,2,1): Param.CH1=MID$(STR,1,1)
    Param.W=W*2
  END IF
  Param.FONT=FONT: Param.H=H
  Param.TOP=0: Param.BOTTOM=H-1
  Param.ATTR=ATTR: Param.BUFLEN=16384
  DS=VARSEG(Param):ES=VARSEG(BITMAP(0))
  SI=VARPTR(Param):DI=VARPTR(BITMAP(0))
  DEF SEG=VARSEG(ASM(0))
  CALL ABSOLUTE(BYVAL ES,BYVAL DI,BYVAL DS,BYVAL SI,VARPTR(ASM(0))+ASM(1))
  DEF SEG
  W=Param.W
END SUB
SUB DrawChar(W AS INTEGER, H AS INTEGER, X AS INTEGER, Y AS INTEGER, C AS INTEGER, B() AS INTEGER)
  STATIC DS,SI
  DS=VARSEG(B(0)):SI=VARPTR(B(0))
  DEF SEG=VARSEG(ASM(0))
  CALL ABSOLUTE(BYVAL DS,BYVAL SI,BYVAL H,BYVAL W,BYVAL Y,BYVAL X,BYVAL C,VARPTR(ASM(0))+ASM(2))
  DEF SEG
END SUB
SUB ParseFontParam(S AS STRING)
  STATIC CMD AS STRING, PARAM AS STRING, IDX AS INTEGER
  IDX=1:WHILE IDX<=LEN(S)
  DO
    CMD=MID$(S,IDX,1): IDX=IDX+1
  LOOP WHILE INSTR("-|@=#^(",CMD)=0
  ISTART=IDX
  WHILE IDX<=LEN(S) AND INSTR("0123456789,",MID$(S,IDX,1))<>0
    IDX=IDX+1
  WEND
  PARAM=MID$(S,ISTART,IDX-ISTART)
  SELECT CASE CMD
    CASE "|"
      FFMT.Y=VAL(PARAM)
    CASE "-"
      FFMT.X=VAL(PARAM)
    CASE "@"
      SEP=INSTR(PARAM,",")
      IF SEP=0 THEN
        FFMT.W=VAL(PARAM)/2: FFMT.H=VAL(PARAM)
      ELSE
        FFMT.W=VAL(MID$(PARAM,1,SEP-1)): FFMT.H=VAL(MID$(PARAM,SEP+1))
      END IF
    CASE "="
      SEP=INSTR(PARAM,",")
      IF SEP=0 THEN
        FFMT.FASC=VAL(PARAM)
      ELSE
        FFMT.FASC=VAL(MID$(PARAM,1,SEP-1)): FFMT.FHZK=VAL(MID$(PARAM,SEP+1))
      END IF
    CASE "#"
      FFMT.ATTR=VAL(PARAM)
    CASE "^"
      FFMT.SP=VAL(PARAM)
    CASE "("
      FFMT.CL=VAL(PARAM)
  END SELECT
  WEND
END SUB
SUB DisplayData(DT AS STRING)
  STATIC PRM AS STRING, TEXTDISP AS STRING
  IF INSTR(DT,"$$")=0 THEN CALL TX(DT): EXIT SUB
  SEP=INSTR(3,DT,":")
  IF SEP=0 THEN
    CALL ParseFontParam(MID$(DT,3))
    EXIT SUB
  ELSE
    CALL ParseFontParam(MID$(DT,3,SEP-3))
    TEXTDISP=MID$(DT,SEP+1)
    CALL DrawStr(TEXTDISP,FFMT.X,FFMT.Y,FFMT.W,FFMT.H,FFMT.FASC,FFMT.FHZK,FFMT.ATTR,FFMT.SP,FFMT.CL)
  END IF
END SUB
SUB DisplaySlide (ID AS INTEGER)
  STATIC M$,I,S,E
  S=SLIDE(ID).LSTART
  E=SLIDE(ID).LEND
  IF S<E THEN
    FOR I = S TO E-1
      L=LEN(CONTENT(I)): P=1: DO: P2=INSTR(P+1,CONTENT(I),CHR$(&H0A))
        IF P2>0 THEN
          DT$=MID$(CONTENT(I),P,P2-P): P=P2+1
        ELSE
          DT$=MID$(CONTENT(I),P): P=0
        END IF
        CALL DisplayData(DT$)
      LOOP UNTIL (P=0)
    NEXT I
  END IF
  PLAY "MB MN T120 O4"
  CALL CloseWAV(PWAV)
  IF WAV(ID)<>"" THEN CALL LoadWAV(PWAV,SB,WAV(ID))

  NOTECUR=0:NOTELEN=0:M$=MUSIC(ID):NCNT=0
  FOR I=0 TO 64:NOTES(I)="":NEXT I
  I=1:WHILE I<=LEN(M$)
    C$=MID$(M$,I,1):C2$=MID$(M$,I,2)
    IF C2$="MF" OR C2$="MB" OR C2$="MN" OR C2$="ML" OR C2$="MS" THEN
      C$=C2$
      I=I+2
    ELSE
      I=I+1
    END IF
    IF C$<>" " THEN
      IF INSTR("CDEFGABcdefgab",C$)>0 THEN NCNT=NCNT+1
      IF NCNT>4 AND NOTELEN<63 THEN NOTELEN=NOTELEN+1:NCNT=0
      NOTES(NOTELEN)=NOTES(NOTELEN)+C$
    END IF
  WEND
  IF LEN(NOTES(NOTELEN))>0 THEN NOTELEN=NOTELEN+1
END SUB

FUNCTION GetSlideIdx% (n AS STRING)
  GetSlideIdx% = -1
  FOR I = 0 TO SLIDECNT-1 STEP 1
    IF RTRIM$(n) = RTRIM$(SLIDE(I).SNAME) THEN
      GetSlideIdx% = I: EXIT FUNCTION
    END IF
  NEXT I
END FUNCTION

SUB ProcessLine (MODE AS STRING, DATATEXT AS STRING)
  SELECT CASE MODE
    CASE ".INIT"
      INITCMD = INITCMD + DATATEXT
    CASE ".CONTENT"
      E=SLIDE(SLIDECNT-1).LEND
      IF SLIDE(SLIDECNT-1).LSTART=E THEN
        CONTENT(E) = DATATEXT
        SLIDE(SLIDECNT-1).LEND=E+1
      ELSEIF LEN(CONTENT(E-1)) + LEN(DATATEXT) < 256 THEN
        CONTENT(E-1) = CONTENT(E-1) + CHR$(&H0A) + DATATEXT
      ELSE
        CONTENT(E) = DATATEXT
        SLIDE(SLIDECNT-1).LEND=E+1
      END IF
    CASE ".MUSIC"
      MUSIC(SLIDECNT - 1) = MUSIC(SLIDECNT - 1) + DATATEXT
    CASE ".WAV"
      WAV(SLIDECNT - 1) = DATATEXT
    CASE ".ACTION"
      ACTIONCNT = ACTIONCNT + 1: I = ACTIONCNT - 1
      ACTIONARR(I).SLIDE = SLIDECNT - 1
      ACTIONARR(I).TRIGGER = LEFT$(DATATEXT, INSTR(DATATEXT, ":") - 1)
      TARGET$ = RIGHT$(DATATEXT, LEN(DATATEXT) - INSTR(DATATEXT, ":"))
      IF INSTR(TARGET$, "EXT$")=1 THEN
        ACTIONARR(I).EXTERNAL = 1
        ACTIONARR(I).TARGET = RIGHT$(TARGET$, LEN(TARGET$) - 4)
      ELSE
        ACTIONARR(I).EXTERNAL = 0
        ACTIONARR(I).TARGET = TARGET$
      END IF
  END SELECT
END SUB

SUB LoadData (FILENAME AS STRING)
  STATIC INITOK AS INTEGER,FILENUM AS INTEGER
  INITOK=0
  INITCMD = ""
  SLIDECNT = 0
  ACTIONCNT = 0
  CURRENTSLIDE = 0
  ACTION$ = ""
  FILENUM=FREEFILE
  CALL CloseWAV(PWAV)
  OPEN FILENAME FOR INPUT AS FILENUM
  MODE$ = ""
  DO
    LINE INPUT #FILENUM, DATATEXT$
    DATATEXT$ = LTRIM$(RTRIM$(DATATEXT$))
    SELECT CASE DATATEXT$
      CASE ""
      CASE ".SLIDE"
        IF SLIDECNT>=SLIDEMAX THEN PANIC("最多支持65页")
        SLIDECNT = SLIDECNT + 1
        I=SLIDECNT-1
        MUSIC(I)="":WAV(I)=""
        LINE INPUT #FILENUM, DATATEXT$
        SLIDE(I).SNAME=DATATEXT$
        IF I=0 THEN
          SLIDE(I).LSTART=0
        ELSE
          SLIDE(I).LSTART=SLIDE(I-1).LEND
        END IF
        SLIDE(I).LEND=SLIDE(I).LSTART
        LCOUNT=0
      CASE ".INIT"
        IF INITOK=1 THEN PANIC("只允许出现1次.INIT")
        INITOK=1
        MODE$ = DATATEXT$
      CASE ".CONTENT"
        MODE$ = DATATEXT$
      CASE ".ACTION"
        MODE$ = DATATEXT$
      CASE ".MUSIC"
        MODE$ = DATATEXT$
      CASE ".WAV"
        MODE$ = DATATEXT$
      CASE ELSE
        CALL ProcessLine(MODE$, DATATEXT$)
    END SELECT
  LOOP UNTIL (EOF(FILENUM))
  CLOSE FILENUM
END SUB

SUB MainLoop
  ON ERROR GOTO ERROREND
  TX (INITCMD)
  CALL DisplaySlide(CURRENTSLIDE)
  DO
    'Handle keyboard action
    K$="":WHILE K$=""
      CALL PlayWAVTask(PWAV,SB)
      K$=INKEY$
      IF NOTECUR<NOTELEN AND PLAY(0)<2 THEN
        PLAY "X"+VARPTR$(NOTES(NOTECUR)):NOTECUR=NOTECUR+1
      END IF
    WEND
    SELECT CASE K$
      CASE " "
          K$ = "SPACE"
      CASE CHR$(27)
          K$ = "ESC"
      CASE CHR$(9)
          K$ = "TAB"
      CASE CHR$(0) + "G"
          K$ = "HOME"
      CASE CHR$(0) + "O"
          K$ = "END"
      CASE CHR$(0) + "I"
          K$ = "PAGEUP"
      CASE CHR$(0) + "Q"
          K$ = "PAGEDOWN"
      CASE CHR$(0) + "H"
          K$ = "UP"
      CASE CHR$(0) + "P"
          K$ = "DOWN"
      CASE CHR$(0) + "K"
          K$ = "LEFT"
      CASE CHR$(0) + "M"
          K$ = "RIGHT"
      CASE CHR$(0) + "R"
          K$ = "INSERT"
      CASE CHR$(0) + "S"
          K$ = "DELETE"
      CASE CHR$(0) + ";"
          K$ = "F1"
      CASE CHR$(0) + "<"
          K$ = "F2"
      CASE CHR$(0) + "="
          K$ = "F3"
      CASE CHR$(0) + ">"
          K$ = "F4"
      CASE CHR$(0) + "?"
          K$ = "F5"
      CASE CHR$(0) + "@"
          K$ = "F6"
      CASE CHR$(0) + "A"
          K$ = "F7"
      CASE CHR$(0) + "B"
          K$ = "F8"
      CASE CHR$(0) + "C"
          K$ = "F9"
      CASE CHR$(0) + "D"
          K$ = "F10"
      CASE CHR$(0) + CHR$(133)
          K$ = "F11"
      CASE CHR$(0) + CHR$(134)
          K$ = "F12"
    END SELECT

    SELECT CASE K$
      CASE ""
      CASE "ESC"
          EXIT SUB
      CASE "HOME"
          CURRENTSLIDE = 0: ACTION$ = "SWITCHSLIDE"
      CASE "END"
          CURRENTSLIDE = SLIDECNT - 1: ACTION$ = "SWITCHSLIDE"
      CASE "PAGEUP"
          IF CURRENTSLIDE > 0 THEN CURRENTSLIDE = CURRENTSLIDE - 1: ACTION$ = "SWITCHSLIDE"
      CASE "PAGEDOWN"
          IF CURRENTSLIDE < SLIDECNT - 1 THEN CURRENTSLIDE = CURRENTSLIDE + 1: ACTION$ = "SWITCHSLIDE"
      CASE ELSE
          CALL HandleKeyboard(K$)
    END SELECT

    SELECT CASE ACTION$
      CASE "SWITCHSLIDE"
          ACTION$ = ""
          CALL DisplaySlide(CURRENTSLIDE)
      CASE "LOADPPT"
          ACTION$ = ""
          CALL LoadData(FILENAME$)
          CALL DisplaySlide(CURRENTSLIDE)
    END SELECT
    ACTION$ = ""
  LOOP
END SUB

FUNCTION n$ (A AS INTEGER)
n$ = LTRIM$(RTRIM$(STR$(A)))
END FUNCTION

SUB HandleKeyboard (K AS STRING)
    KEY$ = K
    IF ACTIONCNT > 0 THEN
        FOR I = 0 TO ACTIONCNT - 1 STEP 1
            IF ACTIONARR(I).SLIDE = CURRENTSLIDE THEN
                TRIGGER$ = RTRIM$(ACTIONARR(I).TRIGGER)
                IF TRIGGER$ = KEY$ THEN
                    CALL HandleTarget(ACTIONARR(I).TARGET, ACTIONARR(I).EXTERNAL)
                    EXIT SUB
                END IF
            END IF
        NEXT I
        FOR I = 0 TO ACTIONCNT - 1 STEP 1
            IF ACTIONARR(I).SLIDE = CURRENTSLIDE AND "ANYKEY" = RTRIM$(ACTIONARR(I).TRIGGER) THEN
                CALL HandleTarget(ACTIONARR(I).TARGET, ACTIONARR(I).EXTERNAL)
                EXIT SUB
            END IF
        NEXT I
    END IF
    SELECT CASE KEY$
        CASE "LEFT"
            IF CURRENTSLIDE > 0 THEN CURRENTSLIDE = CURRENTSLIDE - 1: ACTION$ = "SWITCHSLIDE"
        CASE "BACKSPACE"
            IF CURRENTSLIDE > 0 THEN CURRENTSLIDE = CURRENTSLIDE - 1: ACTION$ = "SWITCHSLIDE"
        CASE "RIGHT"
            IF CURRENTSLIDE < SLIDECNT - 1 THEN CURRENTSLIDE = CURRENTSLIDE + 1: ACTION$ = "SWITCHSLIDE"
        CASE "SPACE"
            IF CURRENTSLIDE < SLIDECNT - 1 THEN CURRENTSLIDE = CURRENTSLIDE + 1: ACTION$ = "SWITCHSLIDE"
    END SELECT
END SUB

SUB HandleTarget (TARGET AS STRING, Ext AS INTEGER)
    IF "NULL" = TARGET THEN EXIT SUB
    IF Ext <> 0 THEN
        ACTION$ = "LOADPPT"
        FILENAME$ = LTRIM$(RTRIM$(TARGET))
    ELSE
        TARGETSLIDE = GetSlideIdx%(TARGET)
        IF TARGETSLIDE > -1 THEN CURRENTSLIDE = TARGETSLIDE: ACTION$ = "SWITCHSLIDE"
    END IF
END SUB

SUB LoadWAV(PWAV AS PLAYWAVType,SB AS SBType,WAVFILE AS STRING)
  STATIC S16
  IF PWAV.FILENUM>0 THEN CLOSE PWAV.FILENUM
  IF SB.STAT<>1 THEN PWAV.FILENUM=-1:PWAV.STAT=-10:EXIT SUB
  PWAV.FILENUM=FREEFILE
  OPEN WAVFILE FOR BINARY AS PWAV.FILENUM
  IF LOF(PWAV.FILENUM)=0 THEN
    CLOSE PWAV.FILENUM:KILL WAVFILE:PWAV.FILENUM=-1:EXIT SUB
  END IF
  GET PWAV.FILENUM,1,PWAV.HDR
  IF UCASE$(PWAV.HDR.RIFFID)<>"RIFF" THEN PWAV.STAT=-1:EXIT SUB
  IF UCASE$(PWAV.HDR.WAVID)<>"WAVE" THEN PWAV.STAT=-2:EXIT SUB
  IF PWAV.HDR.FMTTAG<>1 THEN PWAV.STAT=-3:EXIT SUB 'Not PCM format
  IF PWAV.HDR.FMTSPEC=16 THEN CALL SetMode(SB,1) ELSE CALL SetMode(SB,0)
  PWAV.STAT=0
END SUB

SUB CloseWAV(PWAV AS PLAYWAVType)
  IF PWAV.FILENUM>0 THEN CLOSE PWAV.FILENUM:PWAV.FILENUM=-1
END SUB

SUB PlayWAVTask(PWAV AS PLAYWAVType,SB AS SBType)
  STATIC DATALEN AS INTEGER,BUFADDR AS LONG
  IF PWAV.STAT=2 OR PWAV.STAT<0 OR PWAV.FILENUM<=0 OR SB.STAT<>1 THEN EXIT SUB
  IF PWAV.STAT=1 AND DMADone(SB)=0 THEN EXIT SUB
  IF PWAV.STAT=0 THEN
    PWAV.STAT=1
    PWAV.BUFUSE=0
    SEEK PWAV.FILENUM,WAVHeaderSize
    PWAV.FPOS=LOC(PWAV.FILENUM)
    GET PWAV.FILENUM,,PWAV.BUF0
    PWAV.BUF0DLEN=LOC(PWAV.FILENUM)-PWAV.FPOS
    PWAV.FPOS=LOC(PWAV.FILENUM)
  END IF
  IF PWAV.BUFUSE=1 THEN
    BUFADDR=INT2ULONG&(VARSEG(PWAV.BUF1))*16+INT2ULONG&(VARPTR(PWAV.BUF1))
    DATALEN=PWAV.BUF1DLEN-1
  ELSE
    BUFADDR=INT2ULONG&(VARSEG(PWAV.BUF0))*16+INT2ULONG&(VARPTR(PWAV.BUF0))
    DATALEN=PWAV.BUF0DLEN-1
  END IF
  IF DATALEN<0 THEN PWAV.STAT=2:EXIT SUB

  IF SB.S16 THEN
    OUT &HD8,0 'clear flip flop
    OUT &HD6,SB.MODEREG
    OUT SB.ADDPORT,(BUFADDR\2) AND 255
    OUT SB.ADDPORT,(BUFADDR\512) AND 255
    OUT SB.PGPORT,(BUFADDR\131072)*2
    OUT SB.LENPORT,(DATALEN\2) AND 255
    OUT SB.LENPORT,(DATALEN\512) AND 255
    OUT &HD4, SB.HDMA-4
  ELSE
    OUT &HA,&H4+SB.DMA
    OUT &HC,&H0
    OUT &HB,SB.MODEREG
    OUT SB.ADDPORT,(BUFADDR) AND 255
    OUT SB.ADDPORT,(BUFADDR\256) AND 255
    OUT SB.PGPORT,BUFADDR\65536
    OUT SB.LENPORT,DATALEN AND 255
    OUT SB.LENPORT,(DATALEN\256) AND 255
    OUT &HA,SB.DMA
  END IF
  CALL WriteDSP(SB,&H41)
  CALL WriteDSP(SB,PWAV.HDR.FREQ\256)
  CALL WriteDSP(SB,PWAV.HDR.FREQ AND 255)
  IF SB.S16 THEN
    CALL WriteDSP(SB,&HB0) '16 bit DAC, single cycle, FIFO off
    IF PWAV.HDR.CHANNELS=2 THEN 'subtract 10h for unsigned
      CALL WriteDSP(SB,&H30) '30h=Mode byte for 16 bit signed stereo
    ELSE
      CALL WriteDSP(SB,&H10) '10h=Mode byte for 16 bit signed mono
    END IF
    CALL WriteDSP(SB,(DATALEN\2) AND 255)
    CALL WriteDSP(SB,(DATALEN\512) AND 255)
  ELSE
    CALL WriteDSP(SB,&HC0) '8 bit DAC, single cycle, FIFO off
    IF PWAV.HDR.CHANNELS=2 THEN
      CALL WriteDSP(SB,&H20) '20h=Mode byte for 8 bit unsigned stereo
    ELSE
      CALL WriteDSP(SB,&H0)  '0h=Mode byte for 8 bit unsigned mono
    END IF
    CALL WriteDSP(SB,(DATALEN) AND 255)
    CALL WriteDSP(SB,(DATALEN\256) AND 255)
  END IF
  PWAV.BUFUSE=(NOT PWAV.BUFUSE) AND 1
  IF PWAV.BUFUSE=1 THEN
    GET PWAV.FILENUM,,PWAV.BUF1
    PWAV.BUF1DLEN=LOC(PWAV.FILENUM)-PWAV.FPOS
  ELSE
    GET PWAV.FILENUM,,PWAV.BUF0
    PWAV.BUF0DLEN=LOC(PWAV.FILENUM)-PWAV.FPOS
  END IF
  PWAV.FPOS=LOC(PWAV.FILENUM)
END SUB

SUB InitSoundCard(SB AS SBType)
  STATIC ENV$
  SB.BASEPORT=220:SB.IRQ=5:SB.DMA=1:SB.HDMA=5
  ENV$=UCASE$(ENVIRON$("BLASTER"))
  FOR I=1 TO LEN(ENV$)
    SELECT CASE MID$(ENV$,I,1)
      CASE "A"
        SB.BASEPORT=VAL("&H"+MID$(ENV$,I+1,3))
      CASE "I"
        SB.IRQ=VAL(MID$(ENV$,I+1,1))
      CASE "D"
        SB.DMA=VAL(MID$(ENV$,I+1,1))
      CASE "H"
        SB.HDMA=VAL(MID$(ENV$,I+1,1))
    END SELECT
  NEXT I
  IF SB.DMA<0 OR SB.DMA>3 OR SB.HDMA<4 OR SB.HDMA>7 THEN
    SB.STAT=0:EXIT SUB 'Invalid Parameter
  END IF
  CALL ResetDSP(SB)
  IF SB.STAT=-1 THEN EXIT SUB 'No sound card
  CALL WriteDSP(SB,&HE1)
  SB.VER0=ReadDSP(SB)
  SB.VER1=ReadDSP(SB)
  IF SB.VER0<4 THEN SB.STAT=-2:EXIT SUB 'Pre-SB16 sound card not supported
END SUB

SUB SetMode(SB AS SBType,S16 AS INTEGER)
  IF S16>0 THEN
    SB.S16=1
    SELECT CASE SB.HDMA
      CASE 4
        SB.PGPORT=&H0:SB.ADDPORT=&HC0:SB.LENPORT=&HC2:SB.MODEREG=&H48
      CASE 5
        SB.PGPORT=&H8B:SB.ADDPORT=&HC4:SB.LENPORT=&HC6:SB.MODEREG=&H49
      CASE 6
        SB.PGPORT=&H89:SB.ADDPORT=&HC8:SB.LENPORT=&HCA:SB.MODEREG=&H4A
      CASE 7
        SB.PGPORT=&H8A:SB.ADDPORT=&HCC:SB.LENPORT=&HCE:SB.MODEREG=&H4B
      CASE ELSE
        EXIT SUB
    END SELECT
  ELSE
    SB.S16=0
    SELECT CASE SB.DMA
      CASE 0
        SB.PGPORT=&H87:SB.ADDPORT=&H0:SB.LENPORT=&H1:SB.MODEREG=&H48
      CASE 1
        SB.PGPORT=&H83:SB.ADDPORT=&H2:SB.LENPORT=&H3:SB.MODEREG=&H49
      CASE 2
        SB.PGPORT=&H81:SB.ADDPORT=&H4:SB.LENPORT=&H5:SB.MODEREG=&H4A
      CASE 3
        SB.PGPORT=&H82:SB.ADDPORT=&H6:SB.LENPORT=&H7:SB.MODEREG=&H4B
      CASE ELSE
        EXIT SUB
    END SELECT
  END IF
END SUB

SUB ResetDSP(SB AS SBType)
  SB.STAT=-1
  OUT SB.BASEPORT+&H6,1:CT=0
  DO
    OUT SB.BASEPORT+&H6,0
    STAT=INP(SB.BASEPORT+&HE)
    STAT=INP(SB.BASEPORT+&HA)
    IF STAT=&HAA THEN SB.STAT=1:EXIT DO
    CT=CT+1
  LOOP WHILE CT<100
END SUB

SUB WriteDSP(SB AS SBType,BYTE AS INTEGER)
  WHILE INP(SB.BASEPORT+12) AND &H80:WEND
  OUT SB.BASEPORT+12,BYTE
END SUB

FUNCTION ReadDSP(SB AS SBType)
  STATIC DSPIN AS INTEGER
  WAIT (SB.BASEPORT+&HE),&H80
  DO: DSPIN=INP(SB.BASEPORT+10): LOOP UNTIL DSPIN<>&HAA
  ReadDSP=DSPIN
END FUNCTION

FUNCTION DMADone(SB AS SBType)
  STATIC LO AS INTEGER,HI AS INTEGER,ACK AS INTEGER
  LO=INP(SB.LENPORT):HI=INP(SB.LENPORT)
  IF HI=255 AND LO=255 THEN ACK=INP(SB.BASEPORT+&HF):DMADone=-1 ELSE DMADone=0
END FUNCTION

FUNCTION INT2ULONG&(V AS INTEGER)
  IF V<0 THEN INT2ULONG&=CLNG(V+65536) ELSE INT2ULONG&=CLNG(V)
END FUNCTION

SUB PANIC(MSG AS STRING)
  TX("KB1,1M3CU1,1"): CLOSE
  CLS:LOCATE 1,1:PRINT MSG
  IF SB.STAT=1 THEN CALL ResetDSP(SB)
  END
END SUB

