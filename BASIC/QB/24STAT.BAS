DEFINT A-Z
DIM SHARED GOAL(100) AS INTEGER,N(4) AS INTEGER
COMMON SHARED FTMP$,FRES$,ANSMAX,IDX,ACT
FTMP$="24STAT.TMP":FRES$="24STAT.CSV"
NMAX=13:IDXMAX=NMAX^4:ANSMAX=99:ACT=0:IDX=0
FOR I=0 TO 3:N(I)=1:NEXT I

ON ERROR GOTO ErrorHandler
OPEN FTMP$ FOR INPUT AS #1
ON ERROR GOTO 0
IF ACT=0 THEN
  INPUT #1,N(0),N(1),N(2),N(3),IDX
  FOR I=0 TO ANSMAX:INPUT #1,GOAL(I):NEXT I
  CLOSE #1
END IF
ACT=0

ON TIMER(10) GOSUB SaveHandler
TIMER ON
KEY 15,CHR$(0)+CHR$(1)
ON KEY(15) GOSUB QuitHandler
KEY(15) ON

WHILE N(0)<=NMAX
  CALL CALC24(N()):IDX=IDX+1
  LOCATE ,1:PRINT IDX;"/";IDXMAX;
  N(3)=N(3)+1
  IF N(3)>NMAX THEN
    N(2)=N(2)+1
    N(3)=1
    IF N(2)>NMAX THEN
      N(1)=N(1)+1
      N(2)=1
      IF N(1)>NMAX THEN
        N(0)=N(0)+1
        N(1)=1
      END IF
    END IF
  END IF
  IF ACT=1 THEN CALL SNAPSHOT
WEND
OPEN FRES$ FOR OUTPUT AS #1
PRINT #1,"Goal,Solvable In 1820"
FOR I=0 TO ANSMAX
  PRINT #1,LTRIM$(RTRIM$(STR$(I)));",";LTRIM$(RTRIM$(STR$(GOAL(I))))
NEXT I
CLOSE #1
KILL FTMP$
END

ErrorHandler:
ACT=2
RESUME NEXT

SaveHandler:
ACT=1
RETURN

QuitHandler:
IF ACT=0 THEN CALL SNAPSHOT:END
RETURN

SUB SNAPSHOT
  ACT=2
  OPEN FTMP$ FOR OUTPUT AS #1
  PRINT #1,N(0);N(1);N(2);N(3);IDX
  FOR I=0 TO ANSMAX:PRINT #1,GOAL(I):NEXT I
  CLOSE #1
  ACT=0
END SUB

SUB WRITERES(R AS DOUBLE)
  DIM N AS INTEGER
  IF R>ANSMAX+0.000001 OR R<-0.000001 THEN EXIT SUB
  N=INT(R)
  IF ABS(R-N)>0.00001 THEN EXIT SUB
  GOAL(N)=GOAL(N)+1
END SUB

FUNCTION CALC#(X AS DOUBLE,OP AS INTEGER,Y AS DOUBLE)
  IF X>=1000000 OR Y>=1000000 THEN CALC#=10000000:EXIT FUNCTION
  SELECT CASE OP
    CASE 0
      CALC#=X+Y
    CASE 1
      CALC#=X-Y
    CASE 2
      CALC#=X*Y
    CASE 3
      IF Y=0 THEN CALC#=10000000 ELSE CALC#=X/Y
  END SELECT
END FUNCTION

SUB CALC24(N() AS INTEGER):
  FOR OP0=0 TO 3:FOR OP1=0 TO 3:FOR OP2=0 TO 3
    A#=N(0):B#=N(1):C#=N(2):D#=N(3)

    REM abcd### a#(b#(c#d))
    R#=CALC#(A#,OP0,CALC#(B#,OP1,CALC#(C#,OP2,D#)))
    CALL WRITERES(R#)

    REM abc#d## a#((b#c)#d)
    R#=CALC#(A#,OP0,CALC#(CALC#(B#,OP1,C#),OP2,D#))
    CALL WRITERES(R#)

    REM abc##d# (a#(b#c))#d
    R#=CALC#(CALC#(A#,OP0,CALC#(B#,OP1,C#)),OP2,D#)
    CALL WRITERES(R#)

    REM ab#cd## (a#b)#(c#d)
    R#=CALC#(CALC#(A#,OP0,B#),OP1,CALC#(C#,OP2,D#))
    CALL WRITERES(R#)

    REM ab#c#d# ((a#b)#c)#d
    R#=CALC#(CALC#(CALC#(A#,OP0,B#),OP1,C#),OP2,D#)
    CALL WRITERES(R#)
  NEXT OP2:NEXT OP1:NEXT OP0
END SUB

