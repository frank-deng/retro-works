10 DEFINT A-Z:NL=200:OPEN "R",#1,"A:\CCLIBJ.DOT",32:FIELD #1,32 AS PX$
20 INPUT "请输入目标文件名：",FO$:IF FO$="" THEN FO$="HANZI.TXT"
30 OPEN FO$ FOR OUTPUT AS #2
40 INPUT "请输入汉字：",T$:IF T$="" THEN READ T$
50 I=1:WHILE I<LEN(T$)
60 V0=ASC(MID$(T$,I,1)):I=I+1:IF V0<161 THEN GOTO 140
70 V1=ASC(MID$(T$,I,1)):I=I+1:IF V1<161 THEN GOTO 140
80 QU=V0-161:WEI=V1-161:IF QU>=15 THEN QU=QU-6
90 GET #1,(QU*94+WEI)+1
100 GOSUB 200:FOR J=1 TO 32
110 IF J=17 THEN GOSUB 200 ELSE IF J>1 THEN PRINT#2,",";
120 PRINT#2,MID$(STR$(ASC(MID$(PX$,J,1))),2);
130 NEXT J
140 WEND:CLOSE:END
200 IF NL>200 THEN PRINT#2,""
210 PRINT#2,MID$(STR$(NL),2);" DATA ";:NL=NL+10:RETURN
300 DATA "新年快乐零壹贰叁肆伍陆柒捌玖拾佰仟万亿"

