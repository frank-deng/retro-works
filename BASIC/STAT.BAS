10 DEFDBL A-Z:DEFINT C,I,J:S=0:C=0:DIM D(2048):F$="STAT.SAV"
20 ON ERROR GOTO 190:OPEN F$ FOR OUTPUT AS #1:ON ERROR GOTO 0
30 PRINT "Input Numbers (Input 'x' to end):":WHILE C<2048:INPUT "> ",S$
40 IF S$="" THEN PRINT "Empty Input":GOTO 80 ELSE IF S$="x" THEN GOTO 90
50 V=VAL(S$):D(C)=V:S=S+V:C=C+1
60 IF C=1 THEN MIN=V:MAX=V ELSE IF V<MIN THEN MIN=V ELSE IF V>MAX THEN MAX=V
70 IF F$<>"" THEN PRINT#1,V
80 WEND
90 CLOSE #1
100 PRINT "CNT";C:PRINT "SUM";S:PRINT "AVG";S/C:PRINT "MIN";MIN:PRINT "MAX";MAX
110 A=S/C:VAR=0:FOR I=0 TO C-1:VAR=VAR+(D(I)-A)^2:NEXT I
120 IF C>1 THEN PRINT "STDDEV";SQR(VAR/C):PRINT "STDDEV2";SQR(VAR/(C-1))
130 FOR I=C-1 TO 0 STEP -1:FOR J=0 TO I-1
140 IF D(J)>D(J+1) THEN T=D(J):D(J)=D(J+1):D(J+1)=T
150 NEXT J:NEXT I
160 PRINT "MID";
170 IF C AND 1 THEN PRINT D(C\2) ELSE PRINT (D(C\2-1)+D(C\2))/2
180 END
190 F$="":RESUME NEXT
200 REM Load from file
210 DEFDBL A-Z:DEFINT C,I,J:S=0:C=0:DIM D(2048):INPUT "Filename:",F$
220 OPEN F$ FOR INPUT AS #1:WHILE NOT EOF(1):INPUT #1,S$:IF S$="" THEN GOTO 250
230 V=VAL(S$):D(C)=V:S=S+V:C=C+1
240 IF C=1 THEN MIN=V:MAX=V ELSE IF V<MIN THEN MIN=V ELSE IF V>MAX THEN MAX=V
250 WEND:CLOSE #1:GOTO 100

