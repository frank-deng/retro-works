10 DEFINT A-Z: DIM B(16),M(256,16)
20 FOR I=0 TO 14:B(I)=2^I:NEXT I:B(15)=&H8000
30 INPUT "File:",F$:OPEN F$ FOR INPUT AS #1
40 LINE INPUT "Search:";S$:S$="*"+S$+"*"
50 M(0,0)=1:LS=LEN(S$):FOR I=1 TO LS
60 IF MID$(S$,I,1)="*" THEN M(0,I\16)=M(0,I\16) OR B(I AND 15) ELSE GOTO 80
70 NEXT I
80 WHILE NOT EOF(1):LINE INPUT#1,T$
90 LT=LEN(T$):FOR I=1 TO LT
100 FOR J=0 TO (LS+16)\16:M(I,J)=0:NEXT J
110 FOR J=1 TO LS:SC$=MID$(S$,J,1)
120 IF SC$="*" THEN GOSUB 200 ELSE IF SC$="?" OR SC$=MID$(T$,I,1) THEN GOSUB 300
130 NEXT J:NEXT I
140 IF (M(LT,LS\16) AND B(LS AND 15))<>0 THEN PRINT T$
150 WEND:END
200 V0=M(I-1,J\16) AND B(J AND 15):V1=M(I,(J-1)\16) AND B((J-1) AND 15)
210 IF V0<>0 OR V1<>0 THEN M(I,J\16)=M(I,J\16) OR B(J AND 15)
220 RETURN
300 V=M(I-1,(J-1)\16) AND B((J-1) AND 15)
310 IF V<>0 THEN M(I,J\16)=M(I,J\16) OR B(J AND 15)
320 RETURN
