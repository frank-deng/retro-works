10 DEFINT A-Z:CNT=0:DIM B(16),TN$(64),T(64,4),PR(64):CPR=0
20 FOR I=0 TO 14:B(I)=2^I:NEXT I:B(15)=&H8000
30 PRINT "Input tasks:"
40 FOR I=0 TO 63:PRINT USING "Task ##";CNT+1;
50 PRINT " Name:";:S$="":WHILE S$="":LINE INPUT S$:WEND
60 IF S$="." THEN GOTO 140 ELSE TN$(I)=S$
70 PRINT SPC(9);"Dep:";:LINE INPUT S$:FIN=0:P0=1
80 WHILE FIN=0:P=INSTR(P0,S$,",")
90 IF P=0 THEN ID$=MID$(S$,P0):FIN=1 ELSE ID$=MID$(S$,P0,P-P0)
100 ID=VAL(ID$):IF ID>0 THEN ID=ID-1:T(I,ID\16)=T(I,ID\16) OR B(ID AND 15)
110 P0=P+1
120 WEND:CNT=CNT+1
130 NEXT I
140 GOSUB 300:R=1:LV=0:WHILE R>0:R=0
150 FOR I=0 TO CNT-1:GOSUB 400:NEXT I
160 IF R>0 THEN FOR I=0 TO CNT-1:GOSUB 500:NEXT I
170 LV=LV+1:WEND
180 FOR I=0 TO CNT-1
190 IF TN$(I)<>"" THEN PRINT "Warning: Cyclular dependency found.":END
200 NEXT I:END
300 IF (CNT AND 15)<>0 THEN RETURN
310 A=CNT\16:M=B(CNT AND 15)-1:FOR I=0 TO CNT-1:T(I,A)=T(I,A) AND M
320 IF A<3 THEN FOR J=A+1 TO 3:T(I,J)=0:NEXT J
330 NEXT I:RETURN
400 IF TN$(I)="" THEN RETURN
410 FOR J=0 TO 3:IF T(I,J)<>0 THEN RETURN
420 NEXT J
430 PRINT SPC(LV);TN$(I):CPR=CPR+1:IF (CPR MOD 23)=0 THEN GOSUB 600
440 TN$(I)="":PR(R)=I:R=R+1:RETURN
500 IF TN$(I)="" THEN RETURN
510 FOR J=0 TO R-1:IDX=PR(J)
520 T(I,IDX\16)=T(I,IDX\16) AND (NOT(B(IDX AND 15)))
530 NEXT J:RETURN
600 PRINT "Press any key to continue.";
610 WHILE INPUT$(1)="":WEND
620 PRINT "":RETURN
