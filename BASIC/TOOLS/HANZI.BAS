10 DEFINT A-Z:NL=200:DIM B(8),ZK$(6),RL(6),DS(24),DT(24),ST$(3)
11 FOR I=0 TO 7:B(I)=2^(7-I):NEXT I:FOR I=0 TO 5:READ ZK$(I),RL(I):NEXT I
13 DATA "A:\CCLIBJ.DOT",32,"A:\HZK12",24,"A:\HZK24S",72
14 DATA "A:\HZK24F",72,"A:\HZK24H",72,"A:\HZK24K",72
20 INPUT "请输入目标文件名，字库编号：",FO$,FT:OPEN FO$ FOR OUTPUT AS #1
40 OPEN "R",#2,ZK$(FT),RL(FT):FIELD #2,RL(FT) AS PX$
50 IF FT>=2 AND FT<=5 THEN OPEN "R",#3,"A:\HZK24T",72:FIELD #3,72 AS PXT$
60 INPUT "请输入汉字：",T$:LN=LEN(T$):FOR I=1 TO LN:IF I=LN THEN CLOSE:END
70 V0=ASC(MID$(T$,I,1)):IF V0<161 THEN GOTO 110
80 I=I+1:V1=ASC(MID$(T$,I,1)):IF V1<161 THEN GOTO 110
90 QU=V0-161:WEI=V1-161
100 IF FT=1 THEN GOSUB 300 ELSE IF FT>=2 AND FT<=5 THEN GOSUB 400 ELSE GOSUB 200
110 NEXT I:CLOSE:END
150 IF NL>200 THEN PRINT#1,""
151 PRINT #1,MID$(STR$(NL),2);" DATA ";:NL=NL+10:RETURN
200 GOSUB 150:IF QU>=15 THEN QU=QU-6
210 GET #2,(QU*94+WEI)+1:FOR J=1 TO 32 STEP 2:V=0:PV=VARPTR(V)
220 POKE PV,ASC(MID$(PX$,J+1,1)):POKE PV+1,ASC(MID$(PX$,J,1))
230 IF J>1 THEN PRINT#1,",";
240 C$=STR$(V):IF V>=0 THEN C$=MID$(C$,2)
250 PRINT#1,C$;:NEXT J:RETURN
300 GOSUB 150:GET #2,(QU*94+WEI)+1:FOR J=0 TO 5
310 IF J>0 THEN PRINT#1,",";
320 FOR K=0 TO 3:DS(K)=ASC(MID$(PX$,J*4+K+1,1)):NEXT K
330 DT(0)=DS(0):DT(1)=DS(1) OR (DS(2)\16):DT(2)=((DS(2) AND 15)*16) OR (DS(3)\16)
340 FOR K=0 TO 2:ST$(K)=MID$(STR$(DT(K)),2):NEXT K
350 PRINT#1,ST$(0);",";ST$(1);",";ST$(2);:NEXT J:RETURN
400 IF QU<15 THEN GET #3,(QU*94+WEI)+1 ELSE GET #2,((QU-15)*94+WEI)+1
410 FOR Y0=0 TO 2:FOR J=0 TO 23:DS(J)=ASC(MID$(PX$,J*3+Y0+1,1)):NEXT J
420 GOSUB 150:FOR Y=0 TO 7:FOR G=0 TO 2:V=0:FOR X=0 TO 7
430 IF DS(G*8+X) AND B(Y) THEN V=V OR B(X)
440 NEXT X:DT(G+Y*3)=V:NEXT G:NEXT Y
450 FOR J=0 TO 23: IF J>0 THEN PRINT#1,",";
460 PRINT#1,MID$(STR$(DT(J)),2);:NEXT J
470 NEXT Y0:RETURN

