10 DEFINT A-Z:KEY OFF:RANDOMIZE TIMER
11 DIM SX(100),SY(100),EX(100),EY(100):IL=16
14 SCREEN 1:COLOR 0,15:CLS:P=0:L=IL:D=1
16 FOR I=0 TO IL:SX(I)=0:SY(I)=0:EX(I)=0:EY(I)=0:NEXT I
18 SX(0)=INT(RND*160):SY(0)=INT(RND*100)
20 EX(0)=INT(RND*160)+160:EY(0)=INT(RND*100)+100
30 SX(IL)=SX(0):SY(IL)=SY(0):EX(IL)=EX(0):EY(IL)=EY(0)
32 MSX=1:MSY=1:MEX=1:MEY=1:T0#=TIMER
40 WHILE LEN(INKEY$)=0:C=INT(RND*3)+1
42 LINE (SX(D),SY(D))-(EX(D),EY(D)),0
43 LINE (SX(P),SY(P))-(EX(P),EY(P)),C
50 P=P+1:IF P>IL THEN P=0
52 D=D+1:IF D>IL THEN D=0
54 L=L+1:IF L>IL THEN L=0
60 SX(P)=SX(L)+MSX:IF SX(P)<0 THEN SX(P)=-SX(P):MSX=-MSX ELSE IF SX(P)>319 THEN SX(P)=319*2-SX(P):MSX=-MSX
62 SY(P)=SY(L)+MSY:IF SY(P)<0 THEN SY(P)=-SY(P):MSY=-MSY ELSE IF SY(P)>199 THEN SY(P)=199*2-SY(P):MSY=-MSY
64 EX(P)=EX(L)+MEX:IF EX(P)<0 THEN EX(P)=-EX(P):MEX=-MEX ELSE IF EX(P)>319 THEN EX(P)=319*2-EX(P):MEX=-MEX
66 EY(P)=EY(L)+MEY:IF EY(P)<0 THEN EY(P)=-EY(P):MEY=-MEY ELSE IF EY(P)>199 THEN EY(P)=199*2-EY(P):MEY=-MEY
68 MSX=MSX+INT(RND*5)-2:IF MSX<-5 THEN MSX=-5 ELSE IF MSX>5 THEN MSX=5
70 MSY=MSY+INT(RND*5)-2:IF MSY<-5 THEN MSY=-5 ELSE IF MSY>5 THEN MSY=5
72 MEX=MEX+INT(RND*5)-2:IF MEX<-5 THEN MEX=-5 ELSE IF MEX>5 THEN MEX=5
74 MEY=MEY+INT(RND*5)-2:IF MEY<-5 THEN MEY=-5 ELSE IF MEY>5 THEN MEY=5
80 T#=TIMER:WHILE (T#-T0#)<0.016:T#=TIMER:WEND:T0#=T#
99 WEND:CLS:SCREEN 0:WIDTH 80:CLS:END
